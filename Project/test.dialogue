~ start
if GameState.spoken_to == true and GameState.itemFound == false: #If spoken to already but no correct item
	=> what_item
elif GameState.spoken_to == true and GameState.itemFound == true: #If spoken to and have correct item
	=> item_ready
Phist: It is dark. I know I must do this every year, but it’s still kind of creepy... [#npcmood=stern]
Phist: oh... I think I hear someone. [#mood=calm]

Balor: Who goes there... and what do you want?[#npcmood=calm]

Phist: Dia duit, Phist is ainm dom. I am making my way to the surface — can you help me? Más é do thoil é. [#mood=happy]

Balor: The surface... you are a long way from there.[#npcmood=stern]

Phist: Yes, it is a long journey. Can you help me get to the next layer?[#mood=stern][do! GameState.set_spoken_to()]
=> what_item

~ what_item
Balor: I can help, but first you must do something for me.  [#npcmood=calm]
Balor: I have lost something important — my *scian*.[do! GameState.object_sound()]
Balor: If you can find this and bring it to me, I will help you get to the next layer.  
Balor: Will you accept this quest?[#npcmood=blush]

- Yes
	Phist: I accept. I won't let you down! [#mood=calm][do!GameState.set_toggle()]
	=> END
- No
	Phist: No, I don't want to help. [#mood=angry]
	Balor: ...That’s the only way I can help. Come back to me if you change your mind.[#npcmood=happy][do!GameState.set_toggle()]
	=> END


~ searching
if GameState.itemFound: # If item was found
	Phist: I think I have found the scian, maybe I should show Balor. [#mood=happy]] 
else: #Wrong items
	%Phist: Is this it? [#mood=stern]
	%Phist: Scian... I know what that is... I think?[#mood=calm]
	%Phist: Ummmm... hmmmm... [#mood=angry]
	%Phist: It’s creepy here... I’ve got to be brave![#mood=calm]
=> END


~ item_ready
Phist: I think I am ready to show Balor.

- Yes [if GameState.itemUsed]
	=> found_scian
- No
	Phist: Maybe I should search a bit more... [#mood=calm]
	[do!GameState.set_toggle()]
	=> END
- Let me check my bag
	=> item_ready


~ found_scian
Phist: I have found it. [#mood=happy]

Balor: Show me.[#npcmood=angry]

Phist: Here you go. [#mood=blush]

Balor: You found it — *Mo scian*. Go raibh maith agat.[#npcmood=stern]  
Balor: As you fulfilled your side, I will fulfil mine.[#npcmood=shock]

Phist: So, will you help me get to the next layer? [#mood=happy]

Balor: Go through this portal to the next layer.  
Balor: Tell my old friend *“bhí sé ina chara maith agam”* and they will help.  
Balor: Ádh mór agus slán leat. [do! GameState.set_portal()][do!GameState.set_toggle()]
=> END



